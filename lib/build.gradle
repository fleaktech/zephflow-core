plugins {
  id 'antlr'
  alias(libs.plugins.avro)
}

dependencies {
  api project(":api")

  implementation libs.commons.io
  implementation libs.commons.cli
  implementation libs.commons.csv
  implementation libs.aws.s3
  implementation libs.aws.kinesis
  implementation libs.aws.s3.transfer.manager

  implementation libs.aws.kinesis.client
  implementation libs.aws.dynamodb
  implementation libs.aws.cloudwatch

  implementation libs.kafka.clients
  implementation libs.clickhouse.client

  implementation libs.graalvm.sdk
  runtimeOnly(libs.graalvm.polyglot.python) {
    exclude group: 'org.graalvm.truffle', module: 'truffle-enterprise'
  }

  implementation libs.clickhouse.jdbc

  implementation libs.hadoop.common
  implementation libs.hadoop.client
  implementation(libs.hadoop.aws) {
    exclude group: 'software.amazon.awssdk', module: 'bundle'
  }

  implementation libs.delta.kernel.api
  implementation libs.delta.kernel.defaults

  implementation libs.splunk

  testImplementation libs.jackson.dataformat.yaml
  testImplementation libs.testcontainers.clickhouse

  antlr libs.antlr4
}

configurations.all {
  exclude group: 'log4j', module: 'log4j'
  exclude group: 'dnsjava', module: 'dnsjava'
}


generateGrammarSource {
  arguments << "-lib" << "src/main/antlr/io/fleak/zephflow/lib/antlr"
  arguments += ['-visitor', '-package', 'io.fleak.zephflow.lib.antlr']
  maxHeapSize = '64m'
  outputDirectory = project.layout.buildDirectory.dir("generated/sources/main/java").get().asFile
}

generateAvroJava {
  source('src/main/avro')
  outputDir = project.layout.buildDirectory.dir("generated/sources/main/java").get().asFile
}

sourceSets {
  main {
    java {
      srcDirs += project.layout.buildDirectory.dir("generated/sources/main/java").get().asFile
    }
  }
}

tasks.named('compileJava') {
  dependsOn 'generateGrammarSource'
  dependsOn 'generateAvroJava'
}

tasks.named('spotlessJava') {
  mustRunAfter 'generateGrammarSource'
  mustRunAfter 'generateAvroJava'
}

tasks.named('test') {
  useJUnitPlatform()
  testLogging.showStandardStreams = true
}

tasks.named('sourcesJar') {
  dependsOn 'generateGrammarSource'
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}