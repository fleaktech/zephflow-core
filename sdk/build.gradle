plugins {
  alias(libs.plugins.shadow)
  id 'java'
  id 'java-library'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

dependencies {
  api project(":runner")

  testImplementation libs.aws.s3
  testImplementation libs.aws.kinesis
  testImplementation libs.kafka.clients
  testImplementation libs.delta.kernel.api
  testImplementation libs.delta.kernel.defaults
  testImplementation libs.hadoop.common
  testImplementation(libs.hadoop.aws) {
    exclude group: 'software.amazon.awssdk', module: 'bundle'
  }
}

tasks.named('test') {
  useJUnitPlatform()
  testLogging.showStandardStreams = true
}

shadowJar {
  zip64 true
  archiveClassifier = 'all'
  transform(ServiceFileTransformer) {
  }

  // Exclude POM-only GraalVM artifacts (no JARs) while keeping actual implementation JARs
  dependencies {
    exclude(dependency('org.graalvm.polyglot:python'))
    exclude(dependency('org.graalvm.python:python'))
    exclude(dependency('org.graalvm.python:python-community'))
  }
}

// Disable shadowJar when running any :sdk:*publish* task
gradle.taskGraph.whenReady { taskGraph ->
  def hasSdkPublishTask = taskGraph.allTasks.any { task ->
    task.path.startsWith(':sdk:') && task.name.toLowerCase().contains('publish')
  }

  if (hasSdkPublishTask) {
    tasks.shadowJar.enabled = false
  }
}