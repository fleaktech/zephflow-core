buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // Force JGit 6.x for JReleaser compatibility (JGit 7.x removed GpgObjectSigner)
    classpath('org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r!!') {
      because 'JReleaser 1.21.0 incompatible with JGit 7.x API changes'
    }

    constraints {
      classpath(libs.spring.core) {
        because 'The license plugin brings in Spring 3.x, which conflicts with the Spring Boot 3.x plugin.'
      }
    }
  }
}

plugins {
  alias(libs.plugins.nebula.dependency.lock)
  alias(libs.plugins.nebula.maven.publish)
  alias(libs.plugins.nebula.release)
  id 'idea'
  alias(libs.plugins.spotless)
  alias(libs.plugins.versions)
  id 'jacoco'
  id 'signing'
  alias(libs.plugins.jreleaser)
}


allprojects {
  group 'io.fleak.zephflow'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'com.netflix.nebula.dependency-lock'
  apply plugin: 'nebula.release'
  apply plugin: 'jacoco'

  repositories {
    mavenCentral()
    maven {
      url "https://splunk.jfrog.io/splunk/ext-releases-local"
    }
  }

  configurations.all {
    resolutionStrategy.capabilitiesResolution.withCapability('org.lz4:lz4-java') {
      select('at.yawk.lz4:lz4-java:1.10.3')
    }
  }

  ext {
    licenseName = 'Apache License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
  }

  java {
    sourceCompatibility = libs.versions.java.get()
    targetCompatibility = libs.versions.java.get()
  }
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  test {
    environment "AWS_ACCESS_KEY_ID", "test"
    environment "AWS_SECRET_ACCESS_KEY", "test"
    systemProperty "testcontainers.reuse.enable", "false"
  }

  jacocoTestReport {
    reports {
      xml.required = true
      html.required = true
    }
  }
}

jreleaser {
  gitRootSearch = false

  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
          maxRetries = 300
          connectTimeout = 600 // Timeout in seconds
          readTimeout = 1800    // Timeout in seconds
        }
      }
    }
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'com.netflix.nebula.maven-base-publish'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'signing'

  description = "Zephflow Core - ${project.name.capitalize()} Module"

  java {
    withSourcesJar()
    withJavadocJar()
  }

  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        pom {
          name = project.name
          url = 'https://github.com/fleaktech/zephflow-core'

          licenses {
            license {
              name = licenseName
              url = licenseUrl
            }
          }

          developers {
            developer {
              id = 'fleaktech'
              name = 'Fleak Tech Team'
              email = 'contact@fleak.ai'
            }
          }

          scm {
            connection = 'scm:git:git://github.com/fleaktech/zephflow-core.git'
            developerConnection = 'scm:git:ssh://github.com/fleaktech/zephflow-core.git'
            url = 'https://github.com/fleaktech/zephflow-core'
          }
        }
      }
    }

    repositories {
      maven {
        url = project.rootProject.layout.buildDirectory.dir('staging-deploy')
      }
    }
  }

  // Signing configuration for Maven Central
  signing {
    def signingKey = findProperty("signing.key") ?: System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = findProperty("signing.password") ?: System.getenv("GPG_PASSPHRASE")

    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava

    // Only require signing for non-SNAPSHOT versions
    required { !project.version.toString().contains('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
  }

  dependencies {
    implementation platform(libs.jackson.bom)
    implementation libs.commons.lang3
    implementation libs.commons.text
    implementation libs.commons.collections4
    implementation libs.guava
    implementation libs.jackson.datatype.jsr310
    implementation libs.jackson.dataformat.xml
    implementation libs.jackson.dataformat.yaml
    implementation libs.opensearch.grok
    implementation libs.log4j.api
    implementation libs.log4j.core
    implementation libs.log4j.slf4j2.impl
    implementation libs.influxdb.java
    implementation libs.influxdb.v2.java

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly libs.junit.platform.launcher
    testImplementation libs.mockito.core
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testImplementation libs.testcontainers.junit.jupiter
    testImplementation libs.testcontainers.postgresql
    testImplementation libs.testcontainers.minio
    testImplementation libs.testcontainers.kafka
    testImplementation libs.testcontainers.localstack
    testImplementation libs.log4j.core
    testImplementation libs.log4j.slf4j2.impl

    constraints {
      implementation(libs.commons.codec)
      implementation(libs.commons.io)
      implementation(libs.slf4j.api)
      implementation(libs.log4j.api)
      implementation(libs.log4j.core)
      testImplementation(libs.hamcrest.core)

      implementation(libs.netty.all)
      implementation(libs.netty.buffer)
      implementation(libs.netty.codec)
      implementation(libs.netty.codec.dns)
      implementation(libs.netty.codec.haproxy)
      implementation(libs.netty.codec.http)
      implementation(libs.netty.codec.http2)
      implementation(libs.netty.codec.memcache)
      implementation(libs.netty.codec.mqtt)
      implementation(libs.netty.codec.redis)
      implementation(libs.netty.codec.smtp)
      implementation(libs.netty.codec.socks)
      implementation(libs.netty.codec.stomp)
      implementation(libs.netty.codec.xml)
      implementation(libs.netty.common)
      implementation(libs.netty.handler)
      implementation(libs.netty.handler.proxy)
      implementation(libs.netty.handler.ssl.ocsp)
      implementation(libs.netty.resolver)
      implementation(libs.netty.resolver.dns)
      implementation(libs.netty.resolver.dns.classes.macos)
      implementation(libs.netty.resolver.dns.native.macos)
      implementation(libs.netty.transport)
      implementation(libs.netty.transport.classes.epoll)
      implementation(libs.netty.transport.classes.kqueue)
      implementation(libs.netty.transport.native.epoll)
      implementation(libs.netty.transport.native.kqueue)
      implementation(libs.netty.transport.native.unix.common)
    }
  }

  spotless {
    java {
      target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
      googleJavaFormat(libs.versions.googleJavaFormat.get())
      licenseHeaderFile rootProject.file('LICENSE_HEADER')
    }
  }
  tasks.named('compileJava') {
    dependsOn 'spotlessCheck'
  }

  task printVersion {
    doLast {
      println version
    }
  }
}